# 模块

## 加载模块

haskell 中使用 `import` 来加载模块，一般定义在代码顶部。
> Prelude 是haskell标准库的一组模块，它缺省自动加载

``` haskell
import Data.List  

numUniques :: (Eq a) => [a] -> Int  
numUniques = length . nub
```

也可以在ghci中使用 :m 加载模块

```haskell
ghci> :m Data.List Data.Map Data.Set
```

部分引入

```haskell
import Data.List (nub, sort)
```

除去部分函数引入其它部分

```haskell
import Data.List hiding (nub)
```

为避免命名冲突，可以有限制的引入, 同时为了使用简单可以加上别名

```haskell
import qualified Data.Map
-- 使用 Data.map.filter

import qualified Data.Map as M
-- 使用 M.filter
```

## Data.List

该主要是用来操作 list 的， 如之前使用过的map 和 filter 都是该模块下的函数。

### intersperse

将前一个元素插入后面每一对元素中间

```haskell
ghci> intersperse '.' "abcdef"
-- => "a.b.c.d.e.f"
ghci> intersperse 0 [1,2,3,4,5,6]  
-- => [1,0,2,0,3,0,4,0,5,0,6]
```

### transpose

可以将嵌套的list行变为列

```haskell
ghci> transpose [[1,2,3],[4,5,6],[7,8,9]]
-- => [[1,4,7],[2,5,8],[3,6,9]]  

ghci> transpose ["hey","there","guys"]
-- => ["htg","ehu","yey","rs","e"]

ghci> map sum $ transpose [[0,3,5,9],[10,0,0,9],[8,5,1,-1]]
-- => [18,8,6,17]
```

### foldl' foldl1'

fold在处理较大list时会出现堆栈溢出，此时可以使用 fold 的严格版本 foldl' 和 foldl1'

### concat

将嵌套的 list 铺平为一个list, 可以通过多次调用来铺平多层嵌套

```haskell
ghci> concat ["foo","bar","car"]  
-- => "foobarcar"
ghci> concat [[3,4,5],[2,3,4],[2,1,1]]  
-- => [3,4,5,2,3,4,2,1,1]
```

### concatMap

与 map 一个 List 之后再 concat 它等价

```haskell
ghci> concatMap (replicate 4) [1..3]  
[1,1,1,1,2,2,2,2,3,3,3,3]
```

### and

仅当list中的值都为True时，返回True

```haskell
ghci> and $ map (>4) [5,6,7,8]  
-- => True  
ghci> and $ map (==4) [4,4,4,3,4]  
-- => False
```

### or

如果list中包含True，就会返回True

```haskell
ghci> or $ map (==4) [2,3,4,5,6,1]  
-- => True
ghci> or $ map (>4) [1,2,3]  
-- => False
```

### any all

any list中有值都满足条件，就返回True  
all list中所有值都满足条件时返回True

```haskell
ghci> any (==4) [2,3,5,6,1,4]  
True  
ghci> all (>4) [6,9,10]  
True  
ghci> all (`elem` ['A'..'Z']) "HEYGUYSwhatsup"  
False  
ghci> any (`elem` ['A'..'Z']) "HEYGUYSwhatsup"  
True
```

### iterate

取一个函数和一个值作参数。它会用该值去调用该函数并用所得的结果再次调用该函数，产生一个无限的 List.

```haskell
ghci> take 10 $ iterate (*2) 1  
[1,2,4,8,16,32,64,128,256,512]  
ghci> take 3 $ iterate (++ "haha") "haha"  
["haha","hahahaha","hahahahahaha"]
```

### splitAt

```haskell
ghci> splitAt 3 "heyman"  
("hey","man")
ghci> splitAt 100 "heyman"  
("heyman","")  
ghci> splitAt (-3) "heyman"  
("","heyman")  
ghci> let (a,b) = splitAt 3 "foobar" in b ++ a  
"barfoo"
```

### takeWhile

从一个 List 中取元素，一旦遇到不符合条件的某元素就停止.

```haskell
ghci> takeWhile (>3) [6,5,4,3,2,1,2,3,4,5,4,3,2,1]  
[6,5,4]  
ghci> takeWhile (/=' ') "This is a sentence"  
"This"

-- 求所有三次方小于 1000 的数的和
-- 无限 List 执行的 filter 永远都不会停止, 所有可以使用takeWhile
ghci> sum $ takeWhile (<10000) $ map (^3) [1..]  
53361
```

### dropWhile

如果list中的元素不满足条件，它就返回 List 的余下部分

```haskell
ghci> dropWhile (/=' ') "This is a sentence"  
" is a sentence"  
ghci> dropWhile (<3) [1,2,2,2,3,4,5,4,3,2,1]  
[3,4,5,4,3,2,1]

-- Tuple 的首项表示股票价格，第二三四项分别表示年,月,日。我们想知道它是在哪天首次突破 $1000 的
ghci> let stock = [(994.4,2008,9,1),(995.2,2008,9,2),(999.2,2008,9,3),(1001.4,2008,9,4),(998.3,2008,9,5)]  
ghci> head (dropWhile (\(val,y,m,d) -> val < 1000) stock)  
(1001.4,2008,9,4)

```

### span break

与 takeWhile dropWhile 类似,只是它返回两个 List  
span 是在条件首次为 False 时断开 List，而 break 则是在条件首次为 True 时断开 List

```haskell
ghci> break (==4) [1,2,3,4,5,6,7]  
([1,2,3],[4,5,6,7])  
ghci> span (/=4) [1,2,3,4,5,6,7]  
([1,2,3],[4,5,6,7])
```

### group

取一个 List 作参数，并将其中相邻并相等的元素各自归类，组成一个个子 List.

```haskell
ghci> group [1,1,1,1,2,2,2,2,3,3,2,2,2,5,6,7]  
[[1,1,1,1],[2,2,2,2],[3,3],[2,2,2],[5],[6],[7]]

-- 将一个list中 相邻且相等的元素分组为多个子数组。

ghci> map (\l@(x:xs) -> (x,length l)) . group . sort $ [1,1,1,1,2,2,2,2,3,3,2,2,2,5,6,7]  
[(1,4),(2,7),(3,2),(5,1),(6,1),(7,1)]
```

### inits tails

与 init 和 tail 类似，只是他们会不断递归自己到什么都不剩。

```haskell
ghci> inits "w00t"  
["","w","w0","w00","w00t"]  
ghci> tails "w00t"  
["w00t","00t","0t","t",""]  
ghci> let w = "w00t" in zip (inits w) (tails w)  
[("","w00t"),("w","00t"),("w0","0t"),("w00","t"),("w00t","")]
```

### isInfixOf

类似 `elem` 只是第一个元素为 list

```haskell
ghci> "cat" `isInfixOf` "im a cat burglar"  
True  
ghci> "Cat" `isInfixOf` "im a cat burglar"  
False
```

### isPrefixOf 与 isSuffixOf

一个list 是否以另一个子list开头或结尾

```haskell
ghci> "hey" `isPrefixOf` "oh hey there!"  
False  
ghci> "there!" `isSuffixOf` "oh hey there!"  
True  
```

### elem 与 notElem

检查一个 List 是否包含或不包含某元素.

### partition

根据限制条件返回两个list,一个满足条件,一个不满足条件

```haskell
ghci> partition (>3) [1,3,5,6,3,2,1,0,3,7]  
([5,6,7],[1,3,3,2,1,0,3])
ghci> span (`elem` ['A'..'Z']) "BOBsidneyMORGANeddy"  
("BOB","sidneyMORGANeddy")
```

### find

根据限制条件返回第一个满足条件的元素，这个元素是一个Maybe值，
Maybe 值可能是 Just something 或 Nothing

```haskell
ghci> find (>4) [1,2,3,4,5,6]  
Just 5  
ghci> find (>9) [1,2,3,4,5,6]  
Nothing  
ghci> :t find  
find :: (a -> Bool) -> [a] -> Maybe a
```

### elemIndex

类似find 和 elem 的合体， 只是返回和元素相等的第一个元素的index, 如果不存在返回Nothing

```haskell
ghci> :t elemIndex  
elemIndex :: (Eq a) => a -> [a] -> Maybe Int  
ghci> 4 `elemIndex` [1,2,3,4,5,6]  
Just 3  
ghci> 10 `elemIndex` [1,2,3,4,5,6]  
Nothing
```

### elemIndices

返回与元素相等的所有元素的index组成的list

```haskell
ghci> ' ' `elemIndices` "Where are the spaces?"  
[5,9,13]
```

### findIndex

与find类似，根据条件返回第一个符合的元素的index

```haskell
ghci> findIndex (==4) [5,3,2,1,6,4]  
Just 5  
ghci> findIndex (==7) [5,3,2,1,6,4]  
Nothing  
```

### findIndices

返回所有符合条件的元素的index组成的list

```haskell
ghci> findIndices (`elem` ['A'..'Z']) "Where Are The Caps?"  
[0,6,10,14]
```

### zip3-7 zipWith3-7

与zip 和zipWith 类似，它们可以加3个到7个list合并到一个二元数组或二元函数中。

```haskell
ghci> zipWith3 (\x y z -> x + y + z) [1,2,3] [4,5,2,2] [2,2,3]  
[7,9,8]  
ghci> zip4 [2,3,3] [2,2,2] [5,5,3] [2,2,2]  
[(2,2,5,2),(3,2,5,2),(3,2,3,2)]
```

### lines unlines

lines 放回由换行符拆成的字符串的list  
unlines 返回和lines相反返回由list通过换行符拼接成的字符串

```haskell
ghci> lines "first line\nsecond line\nthird line"  
["first line","second line","third line"]
```

### words 和 unwords

把一个字串分为一组单词或执行相反的操作

```haskell
ghci> words "hey these are the words in this sentence"
["hey","these","are","the","words","in","this","sentence"]  
ghci> words "hey these are the words in this\nsentence"  
["hey","these","are","the","words","in","this","sentence"]  
ghci> unwords ["hey","there","mate"]  
"hey there mate"
```

### nub

将一个 List 中的重复元素去掉

```haskell
ghci> nub [1,2,3,4,3,2,1,2,3,4,3,2,1]  
[1,2,3,4]  
ghci> nub "Lots of words and stuff"  
"Lots fwrdanu"
```

### delete

删除list中首次出现的该元素。

```haskell
ghci> delete 'h' "hey there ghang!"  
"ey there ghang!"  
ghci> delete 'h' . delete 'h' $ "hey there ghang!"  
"ey tere ghang!"
```

### 函数 \\

类似集合的差积，返回不包含后面list中元素的list

```haskell
ghci> [1..10] \\ [2,5,9]  
[1,3,4,6,7,8,10]  
ghci> "Im a big baby" \\ "big"  
"Im a  baby"
```

### union

类似集合的并集，返回的list中都会包含后面的list

```haskell
ghci> "hey man" `union` "man what's up"  
"hey manwt'sup"  
ghci> [1..7] `union` [5..10]  
[1,2,3,4,5,6,7,8,9,10]
```

### intersection

交集，返回两个list的交集

```haskell
ghci> [1..7] `intersect` [5..10]  
[5,6,7]
```

### insert

insert 可以将一个元素插入一个可排序的 List，并将其置于首个大于等于它的元素之前,如果元素已经排序，返回接口也将是排序的

```haskell
ghci> insert 4 [1,2,3,5,6,7]  
[1,2,3,4,5,6,7]  
ghci> insert 'g' $ ['a'..'f'] ++ ['h'..'z']  
"abcdefghijklmnopqrstuvwxyz"  
ghci> insert 3 [1,2,4,3,2,1]  
[1,2,3,4,3,2,1]
```

### 更多

length，take，drop，splitAt，!! 和 replicate 之类的函数有个共同点。那就是它们的参数中都有个 Int 值（或者返回Int值），我觉得使用 Intergal 或 Num 型别类会更好，但出于历史原因，修改这些会破坏掉许多既有的代码。在 Data.List 中包含了更通用的替代版，如: genericLength，genericTake，genericDrop，genericSplitAt，genericIndex 和 genericReplicate。  

length 的型别声明为 length :: [a] -> Int，而我们若要像这样求它的平均值，let xs = [1..6] in sum xs / length xs ，就会得到一个型别错误，因为 / 运算符不能对 Int 型使用! 而 genericLength 的型别声明则为 genericLength :: (Num a) => [b] -> a，Num 既可以是整数又可以是浮点数，let xs = [1..6] in sum xs / genericLength xs 这样再求平均数就不会有问题  

nub, delete, union, intsect 和 group 函数也有各自的通用替代版 nubBy，deleteBy，unionBy，intersectBy 和 groupBy，它们的区别就是前一组函数使用 (==) 来测试是否相等，而带 By 的那组则取一个函数作参数来判定相等性，group 就与 groupBy (==) 等价.  

```haskell
ghci> let values = [-4.3，-2.4，-1.2，0.4，2.3，5.9，10.5，29.1，5.3，-2.4，-14.5，2.9，2.3]  
ghci> groupBy (\x y -> (x > 0) == (y > 0)) values  
[[-4.3,-2.4,-1.2],[0.4,2.3,5.9,10.5,29.1,5.3],[-2.4,-14.5],[2.9,2.3]]

-- 使用Data.Function 的on可以进一步简化
ghci> groupBy ((==) `on` (> 0)) values  
[[-4.3,-2.4,-1.2],[0.4,2.3,5.9,10.5,29.1,5.3],[-2.4,-14.5],[2.9,2.3]]
```

sort，insert，maximum 和 min 都有各自的通用版本。如 groupBy 类似，sortBy，insertBy，maximumBy 和 minimumBy 都取一个函数来比较两个元素的大小。像 sortBy 的型别声明为: sortBy :: (a -> a -> Ordering) -> [a] -> [a]。前面提过，Ordering 型别可以有三个值,LT，EQ 和 GT。compare 取两个 Ord 型别类的元素作参数，所以 sort 与 sortBy compare 等价.

```haskell
-- 可以根据list的长度来比较list的大小
ghci> let xs = [[5,4,5,4,4],[1,2,3],[3,5,4,3],[],[2],[2,2]]  
ghci> sortBy (compare `on` length) xs  
[[],[2],[2,2],[1,2,3],[3,5,4,3],[5,4,5,4,4]]
```

## Data.Char

isControl 判断一个字符是否是控制字符。 isSpace 判断一个字符是否是空格字符，包括空格，tab，换行符等. isLower 判断一个字符是否为小写. isUper 判断一个字符是否为大写。 isAlpha 判断一个字符是否为字母. isAlphaNum 判断一个字符是否为字母或数字. isPrint 判断一个字符是否是可打印的. isDigit 判断一个字符是否为数字. isOctDigit 判断一个字符是否为八进制数字. isHexDigit 判断一个字符是否为十六进制数字. isLetter 判断一个字符是否为字母. isMark 判断是否为 unicode 注音字符，你如果是法国人就会经常用到的. isNumber 判断一个字符是否为数字. isPunctuation 判断一个字符是否为标点符号. isSymbol判断一个字符是否为货币符号. isSeperater 判断一个字符是否为 unicode 空格或分隔符. isAscii 判断一个字符是否在 unicode 字母表的前 128 位。 isLatin1 判断一个字符是否在 unicode 字母表的前 256 位. isAsciiUpper 判断一个字符是否为大写的 ascii 字符. isAsciiLower 判断一个字符是否为小写的 ascii 字符.  

以上所有判断函数的型别声明皆为 Char -> Bool，用到它们的绝大多数情况都无非就是过滤字串或类似操作

```haskell
ghci> all isAlphaNum "bobby283"  
True  
ghci> all isAlphaNum "eddy the fish!"  
False
```

### isSpace

使用 isSpace 来实现 Data.List 的 words 函数

```haskell
ghci> words "hey guys its me"  
["hey","guys","its","me"]  
ghci> groupBy ((==) `on` isSpace) "hey guys its me"  
["hey"," ","guys"," ","its"," ","me"]  
```

### GeneralCategory

Data.Char 中也有和Ordering 类似的枚举类型, GeneralCategory 包含31种 它可用 generalCategory :: Char -> GeneralCategory 获取得到

```haskell
ghci> generalCategory ' '  
Space  
ghci> generalCategory 'A'  
UppercaseLetter  
ghci> generalCategory 'a'  
LowercaseLetter  
ghci> generalCategory '.'  
OtherPunctuation  
ghci> generalCategory '9'  
DecimalNumber  
ghci> map generalCategory " \t\nA9?|"  
[Space,Control,Control,UppercaseLetter,DecimalNumber,OtherPunctuation,MathSymbol]
```

### toUpper digitToInt toTitle

toUpper 将一个字符转为大写字母，若该字符不是小写字母，就按原值返回. toLower 将一个字符转为小写字母，若该字符不是大写字母，就按原值返回. toTitle 将一个字符转为 title-case，对大多数字元而言，title-case 就是大写. digitToInt 将一个字符转为 Int 值，而这一字符必须得在 '1'..'9','a'..'f'或'A'..'F' 的范围之内.  
intToDigit 是 digitToInt 的反函数。它取一个 0 到 15 的 Int 值作参数，并返回一个小写的字符.

```haskell
ghci> map digitToInt "34538"  
[3,4,5,3,8]  
ghci> map digitToInt "FF85AB"  
[15,15,8,5,10,11]

ghci> intToDigit 15  
'f'  
ghci> intToDigit 5  
'5'
```

### ord char

ord 与 char 函数可以将字符与其对应的数字相互转换.

```haskell
ghci> ord 'a'  
97  
ghci> chr 97  
'a'  
ghci> map ord "abcdefgh"  
[97,98,99,100,101,102,103,104]
```

Caesar ciphar 是加密的基础算法，它将消息中的每个字符都按照特定的字母表进行替换。它的实现非常简单，我们这里就先不管字母表了.

```haskell
encode :: Int -> String -> String  
encode shift msg = 
  let ords = map ord msg  
      shifted = map (+ shift) ords  
  in map chr shifted

ghci> encode 3 "Heeeeey"  
"Khhhhh|"  
ghci> encode 4 "Heeeeey"  
"Liiiii}"  
ghci> encode 1 "abcd"  
"bcde"  
ghci> encode 5 "Marry Christmas! Ho ho ho!"  
"Rfww~%Hmwnxyrfx&%Mt%mt%mt&"
```

decode

```haskell
decode :: Int -> String -> String  
decode shift msg = encode (negate shift) msg
ghci> encode 3 "Im a little teapot"  
"Lp#d#olwwoh#whdsrw"  
ghci> decode 3 "Lp#d#olwwoh#whdsrw"  
"Im a little teapot"  
ghci> decode 5 . encode 5 $ "This is a sentence"  
"This is a sentence"
```

## Data.Map

关联列表(也叫做字典)是按照键值对排列而没有特定顺序的一种 键值对List

```haskell
phoneBook = [("betty","555-2938") ,
             ("bonnie","452-2928") ,
             ("patsy","493-2928") ,
             ("lucille","205-2928") ,
             ("wendy","939-8282") ,
             ("penny","853-2492") ]
```

### findKey

```haskell
findKey :: (Eq k) => k -> [(k,v)] -> v 
findKey key xs = snd . head . filter (\(k,v) -> key == k) $ xs
```

如果找不到

```haskell
findKey :: (Eq k) => k -> [(k,v)] -> Maybe v
findKey key [] = Nothing
findKey key ((k,v):xs) =
     if key == k then
         Just v
     else
         findKey key xs
```

flod 实现

```haskell
findKey :: (Eq k) => k -> [(k,v)] -> Maybe v 
findKey key = foldr (\(k,v) acc -> if key == k then Just v else acc) Nothing
```

```haskell
ghci> findKey "betty" phoneBook 
Just "555-2938" 
ghci> findKey "wilma" phoneBook 
Nothing
```

### fromList

fromList 取一个关联列表，返回一个与之等价的 Map  
若其中存在重复的键,就将其忽略,  
Map 的 key 必须属于 Ord 类型， 如果Key的型别属于 Ord 型别类，就应该尽量使用Data.Map

```haskell
ghci> Map.fromList [("betty","555-2938"),("bonnie","452-2928"),("lucille","205-2928")]
fromList [("betty","555-2938"),("bonnie","452-2928"),("lucille","205-2928")]
ghci> Map.fromList [(1,2),(3,2),(3,2),(5,5)]
fromList [(1,2),(3,2),(5,5)]
```

### Map.empty

返回一个空 map

```haskell
ghci> Map.empty
fromList []
```

### Map.insert

给 map 插入新的键值对

```haskell
ghci> Map.insert 3 100 Map.empty
fromList [(3,100)]
ghci> Map.insert 5 600 (Map.insert 4 200 ( Map.insert 3 100  Map.empty))
fromList [(3,100),(4,200),(5,600)]
```

### size

返回一个 map 的大小。

```haskell
ghci> Map.size Map.empty
0
ghci> Map.size $ Map.fromList [(2,4),(3,3),(4,2),(5,4),(6,4)]
5
```

### singleton

根据传入的 key value 返回包含一个元素的map

```haskell
ghci> Map.singleton 3 9
fromList [(3,9)]
ghci> Map.insert 5 9 $ Map.singleton 3 9
fromList [(3,9),(5,9)]
```

### Map.lookup

Map.lookup :: Ord k => k -> Map k a -> Maybe a

根据key找到map 中的 Value

### member

判断该key是否存在于 map 中

```haskell
ghci> Map.member 3 $ Map.fromList [(3,6),(4,3),(6,9)] 
True 
ghci> Map.member 3 $ Map.fromList [(2,5),(4,5)] 
False
```

### map 与 filter

与 List 版本很相似

```haskell
ghci> Map.map (*100) $ Map.fromList [(1,1),(2,4),(3,9)]
fromList [(1,100),(2,400),(3,900)]
ghci> Map.filter isUpper $ Map.fromList [(1,'a'),(2,'A'),(3,'b'),(4,'B')]
fromList [(2,'A'),(4,'B')]
```

### toList

toList 是 fromList 的反函数。

```haskell
ghci> Map.toList . Map.insert 9 2 $ Map.singleton 4 3 
[(4,3),(9,2)]
```

### keys 与 elems

keys 与 elems 各自返回一组由键或值组成的 List，keys 与 map fst . Map.toList 等价，elems 与 map snd . Map.toList等价. fromListWith 是个很酷的小函数，它与 fromList 很像，只是它不会直接忽略掉重复键，而是交给一个函数来处理它们。

```haskell
phoneBook =
    [("betty","555-2938")  
    ,("betty","342-2492")  
    ,("bonnie","452-2928")  
    ,("patsy","493-2928")  
    ,("patsy","943-2929")  
    ,("patsy","827-9162")  
    ,("lucille","205-2928")  
    ,("wendy","939-8282")  
    ,("penny","853-2492")  
    ,("penny","555-2111")  
    ]

phoneBookToMap :: (Ord k) => [(k, String)] -> Map.Map k String  
phoneBookToMap xs = Map.fromListWith (\number1 number2 -> number1 ++ ", " ++ number2) xs
```

### map更多信息

[data-map](http://www.haskell.org/ghc/docs/latest/html/libraries/containers/Data-Map.html)

## Data.Set

Data.Set 模块提供了对数学中集合的处理。集合既像 List 也像 Map: 它里面的每个元素都是唯一的，且内部的数据由一棵树来组织(这和 Data.Map 模块的 map 很像)，必须得是可排序的。同样是插入,删除,判断从属关系之类的操作，使用集合要比 List 快得多。对一个集合而言，最常见的操作莫过于并集，判断从属或是将集合转为 List.  

由于 Data.Set 模块与 Prelude 模块和 Data.List 模块中存在大量的命名冲突，所以我们使用 qualified import

```haskelll
import qualified Data.Set as Set
```

## 建立自己的模块

文件名和模块名相同，一般以大写开头, 使用 `import Geometry` 来引入

```haskell
module Geometry  
( sphereVolume  
，sphereArea  
，cubeVolume  
，cubeArea  
，cuboidArea  
，cuboidVolume  
) where

sphereVolume :: Float -> Float  
sphereVolume radius = (4.0 / 3.0) * pi * (radius ^ 3)  

sphereArea :: Float -> Float  
sphereArea radius = 4 * pi * (radius ^ 2)  

cubeVolume :: Float -> Float  
cubeVolume side = cuboidVolume side side side  

cubeArea :: Float -> Float  
cubeArea side = cuboidArea side side side  

cuboidVolume :: Float -> Float -> Float -> Float  
cuboidVolume a b c = rectangleArea a b * c  

cuboidArea :: Float -> Float -> Float -> Float  
cuboidArea a b c = rectangleArea a b * 2 + rectangleArea a c * 2 + rectangleArea c b * 2  

rectangleArea :: Float -> Float -> Float  
rectangleArea a b = a * b
```

子模块 Geometry.Cuboid, 在Geometry 文件夹下创建文件 cuboid.hs

```haskell
module Geometry.Cuboid
(
  volume,
  area
) where

volume :: Float -> Float -> Float -> Float
volume a b c = rectangleArea a b * c

area :: Float -> Float -> Float -> Float
area a b c = rectangleArea a b * 2 + rectangleArea a c * 2 + rectangleArea c b * 2

rectangleArea :: Float -> Float -> Float
rectangleArea a b = a * b
```

使用 `import qualified Geometry.Cuboid as Cuboid` 引入来避免重名

```haskell
module Geometry.Cube
(
  volume,
  area
) where

import qualified Geometry.Cuboid as Cuboid

volume :: Float -> Float
volume side = Cuboid.volume side side side

area :: Float -> Float
area side = Cuboid.area side side side

```
