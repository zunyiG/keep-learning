# 输入输出

由于haskell 是一门纯函数式编程语言，函数不能够去改变状态的。 函数唯一能做的就是根据参数返回结果，如果用相同参数去调用同一个函数两次得到的结果是相同的。这在命令式语言中看起来是非常大的限制，但是在函数式给我们带来了很多的益处。

但是同时这也给我们带来了一个问题，我们无法改变现实世界的状态，比如打印一个计算结果。不过haskell设计了一个非常聪明的系统来处理有副作用的函数，它漂亮地将我们的程序区分成纯粹跟非纯粹两部分，非纯粹的部分负责跟键盘还有屏幕沟通。有了这种机制，在跟外界沟通的同时，我们还是能够有效运用纯粹所带来的好处，像是惰性求值、容错性跟模块性。

## hello world

我们可以创建一个文件，包含以下内容

```haskell
main = putStrLn "hello, world"
```

然后通过ghc编译

```haskell
$ ghc --make helloworld
[1 of 1] Compiling Main                 ( helloworld.hs, hellowowlrd.o )
Linking helloworld ...
```

最后在运行, 最后将打印出字串到屏幕的程序

```haskell
$ ./helloworld
hello, world
```

putStrLn 用于向屏幕打印值， 它接受一个String 类型的参数，返回一个IO action， 这个IO action 包含了一个 （）形态即 Tuple

```haskell
ghci> :t putStrLn
putStrLn :: String -> IO ()

ghci> :t putStrLn "hello, world"
putStrLn "hello, world" :: IO ()

```

一个 I/O action 是一个会造成副作用的动作，常是指读取输入或输出到屏幕，同时也代表会回传某些值。在屏幕打印出几个字串并没有什么有意义的回传值可言，所以这边用一个 () 来代表。 一个 I/O action 会在我们把它绑定到 main 这个名字并且执行程序的时候触发。

## do block

把整个程序限制在只能有一个 I/O action 看似是个极大的限制。这就是为什么我们需要 do 表示法来将所有 I/O action 绑成一个。来看看下面这个例子。

```haskell
main = do
    putStrLn "Hello, what's your name?"
    name <- getLine
    putStrLn ("Hey " ++ name ++ ", you rock!")
```

程序每一步都是一个 I/O action。将所有 I/O action 用 do 绑在一起变成了一个大的 I/O action
这个大的 I/O action 的类型是 IO ()，这是由最后一个 I/O action 所决定的。按照惯例，我们通常不会把 main 的型态在程序中写出来。

```haskell
ghci> :t getLine
getLine :: IO String
```

### 取值

getLine 是一个回传 String 的 I/O action, 它会等待用户的输入。  
name <- getLine 这句的作用是执行一个 I/O action getLine 并将它的结果绑定到 name。
要获得 I/O的数据唯一办法就是用 <-， 要从 I/O action 拿出某些数据，就一定同时要在另一个 I/O action 中，这就是 Haskell 如何分开纯跟不纯的程序的方法。

```haskell
main = do
    foo <- putStrLn "Hello, what's your name?"
    name <- getLine
    putStrLn ("Hey " ++ name ++ ", you rock!")
```

每个 I/O action 都有一个值封装在里面，另外最后一个 action 不能绑定任何名字。
foo 只会有一个 () 的值，所以绑定到 foo 这个名字似乎是多余的，所以我们宁愿写成 putStrLn something

你能够在 do blocks 中使用 let 绑定

```haskell
import Data.Char

main = do
    putStrLn "What's your first name?"
    firstName <- getLine
    putStrLn "What's your last name?"
    lastName <- getLine
    let bigFirstName = map toUpper firstName
        bigLastName = map toUpper lastName
    putStrLn $ "hey " ++ bigFirstName ++ " " ++ bigLastName ++ ", how are you?"
```

当你要绑定 I/O actions 的结果时用 <-，而对于纯粹的 expression 使用 let 绑定。对于错误的 let firstName = getLine，我们只不过是把 getLine 这个 I/O actions 给了一个不同的名字罢了， 其实可以将IO 理解为一个容器，它负责存放我们与外界交互的操作

现在我们来写一个会一行一行不断地读取输入，并将读进来的字反过来输出到屏幕上的程序。程序会在输入空白行的时候停止。

```haskell
main = do
    line <- getLine
    if null line
        then return ()
        else do
            putStrLn $ reverseWords line
            main

reverseWords :: String -> String
reverseWords = unwords . map reverse . words
```

我们用 getLine 从终端读取了一行，并把这行输入取名叫 line。然后接着一个条件式 expression。当 if 的条件是 true （也就是输入了一个空白行），我们便执行一个 I/O action，如果 if 的条件是 false，那 else 底下的 I/O action 被执行,由于我们在 else 中只能有一个 I/O action，所以我们用 do 来将两个 I/O actions 绑成一个,

```haskell
else (do
    putStrLn $ reverseWords line
    main)
```

在 do block 里面，我们依序调用了 getLine 以及 reverseWords， 然后递归调用了 main，
由于 main 也是一个 I/O action,调用 main 也就代表我们回到程序的起点。

### return

then 的区块即 then return ()被执行时， 这里 return 在 Haskell 里面的意义跟其它语言的 return 完全不同的。
在 Haskell 中，它的作用是利用某个 pure value 造出 I/O action， 相当于我们把值又装进了IO 容器。

在 I/O do block 中放一个 return 并不会结束执行。像下面这个程序会执行到底。

```haskell
main = do
    return ()
    return "HAHAHA"
    line <- getLine
    return "BLAH BLAH BLAH"
    return 4
    putStrLn line
```

我们能用 <- 与 return 来达到绑定名称的目的。return 与 <- 作用相反。return 把 value 装进盒子中，而 <- 将 value 从盒子拿出来，并绑定一个名称, 不过这样做显得有些多余，应为 你可以用 let 绑定

```haskell
main = do
    a <- return "hell"
    b <- return "yeah!"
    putStrLn $ a ++ " " ++ b
```

## putStr putChar print

在除了 putStrLn 之外，还有很多可以处理 IO 的函数。

```haskell
putStr :: String -> IO ()

putChar :: Char -> IO ()
```

putStr 的边界条件是空字串，所以假设我们打印一个空字串，那它只是回传一个什么都不做的 I/O action，像 return ()。如果打印的不是空字串，那就先用 putChar 打印出字串的第一个字符，然后再用 putStr 打印出字串剩下部份。

```haskell
print :: Show a => a -> IO ()
```

print 接受任何是 Show typeclass 的 instance 的型态的值，这代表我们知道如何用字串表示它，调用 show 来将值变成字串然后将其输出到终端上。它相当于是 putStrLn . show 的组合, GHCI 实际上就是用了 print 来将这些值输出到终端

```haskell
main = do print True
          print 2
          print "haha"
          print 3.2
          print [3,4,3]
```

```haskell
$ runhaskell print_test.hs
True
2
"haha"
3.2
[3,4,3]
```

## getChar

```haskell
getChar :: IO Char
```

getChar 是一个从输入读进一个字符的 I/O action

```haskell
import Control.Monad

main = do
    c <- getChar
    when (c /= ' ') $ do
        putChar c
        main
```

when 这函数可以在 Control.Monad 中找到它, 它看起来像一个控制流的 判断语句， 但它实际上是一个函数， 值是 True，便放回给它的 I/O action。如果值是 False 放回 return ()。 它相当于是 if something then do some I/O action else return ()

## sequence mapM mapM_

```haskell
sequence :: [IO a] -> IO [a]

main = do
    a <- getLine
    b <- getLine
    c <- getLine
    print [a,b,c]

-- 用 sequence 可以改成

main = do
    rs <- sequence [getLine, getLine, getLine]
    print rs
```

sequence 接受一串 I/O action，并回传一个会依序执行它们的 I/O action

一个常见的使用方式是我们将 print 或 putStrLn 之类的函数 map 到串列上。map print [1,2,3,4] 这个动作并不会产生一个 I/O action，而是一串 I/O action，就像是 [print 1, print 2, print 3, print 4]。如果我们将一串 I/O action 变成一个 I/O action，我们必须用 sequence

```haskell
ghci> sequence (map print [1,2,3,4,5])
1
2
3
4
5
[(),(),(),(),()]
```

由于 先map 再 sequence 这个动作太常用了，所以有一些函数在函式库中 mapM 跟 mapM_, mapM_的区别是，它会把运算的结果丢掉，在我们不关心 I/O action 结果的情况下，mapM_ 是最常被使用的。

```haskell
ghci> mapM print [1,2,3]
1
2
3
[(),(),()]
ghci> mapM_ print [1,2,3]
1
2
3
```

## forever

forever 接受一个 I/O action 并回传一个永远作同一件事的 I/O action。你可以在 Control.Monad 中找到它。
下面的程序会不断地要用户输入些东西，并把输入的东西转成大写输出到屏幕上。

```haskell
import Control.Monad
import Data.Char

main = forever $ do
    putStr "Give me some input: "
    l <- getLine
    putStrLn $ map toUpper l
```

在 Control.Monad 中的 forM 跟 mapM 的作用一样，只是参数的顺序相反而已。第一个参数是串列，而第二个则是函数

```haskell
import Control.Monad

main = do
    colors <- forM [1,2,3,4] (\a -> do
        putStrLn $ "Which color do you associate with the number " ++ show a ++ "?"
        color <- getLine
        return color)
    putStrLn "The colors that you associate with 1, 2, 3 and 4 are: "
    mapM putStrLn colors
```

(\a -> do ...) 是接受一个数字并回传一个 I/O action 的函数, 可以把 forM 的意思想成将串列中的每个元素作成一个 I/O action。至于每个 I/O action 实际作什么就要看原本的元素是什么。然后，执行这些 I/O action 并将结果绑定到某个名称上。或是直接将结果忽略掉。

```haskell
$ runhaskell from_test.hs
Which color do you associate with the number 1?
white
Which color do you associate with the number 2?
blue
Which color do you associate with the number 3?
red
Which color do you associate with the number 4?
orange
The colors that you associate with 1, 2, 3 and 4 are:
white
blue
red
orange
```

其实我们也不是一定要用到 forM，只是用了 forM 程序会比较容易理解。正常来讲是我们需要在 map 跟 sequence 的时候定义 I/O action 的时候使用 forM，同样地，我们也可以将最后一行写成 forM colors putStrLn。

不要把像是 putStrLn 的函式想成接受字串并输出到屏幕。要想成一个函式接受字串并回传一个 I/O action。当 I/O action 被执行的时候，会漂亮地打印出你想要的东西。

## 文件与字符流

```haskell
getContents :: IO String
```

getContents 是一个从标准输入读取直到 end-of-file 字符的 I/O action, getContents 是惰性 I/O (Lazy I/O)。当我们写了 foo <- getContents，他并不会马上读取所有输入，将他们存在 memory 里面。他只有当你真的需要输入数据的时候才会读取。
