# 构建 类型 类型类

## 抽象数据类型

我们可以使用 data 关键字来定义 类型

```haskell
-- Bool 在标准库中的定义
data Bool = False | True

-- Int
data Int = -2147483648 | -2147483647 | ... | -1 | 0 | 1 | 2 | ... | 2147483647
```

构造一个表示图形的类型， 假设包含圆和矩形

```haskell
data Shape = Circle Float Float Float | React Float Float Float Float
```

### 构造函数与类型

data 类似java的class，通过等号后面构造函数，创建出对应类型的值对象，它的类型即为等号前面的类型  
一个类型，可以有多个值构造函数, 模式匹配时使用值构造函数进行匹配。  

类型 Circle， React 其实可以理解为构造函数 本质也是一个函数，它的类型为

```haskell
ghci> :t Circle
Circle :: Float -> Float -> Float -> Shape
ghci> :t React
React :: Float -> Float -> Float -> Float -> Shape
```

### 计算图形面积的函数

```haskell
surface :: Shape -> Float
surface (Circle _ _ r) = pi * r ^ 2
surface (React x1 y1 x2 y2) = (abs $ x2 - x1) * (abs $ y2 - y1)
```

> 需要注意的是这里不可以以写成 Circle -> Float 因为 Circle 并不是真正的类型,类似不可以写 True -> False
> 如果使用模式匹配针对的都是构造函数， 类似的包括之前用过的 [], False, 5, 他们都是构造函数只是不包含参数而已

```haskell
surface $ Circle 1 1 1
3.1415927

surface $ React 0 0 200 100
20000.0
```

### deriving 派生

如果要将Circle 输出到控制台， 我们可以让Circle 成为show类型类 的子类型 ( 使用 deriving（派生）)

```haskell
data Shape = Circle Float Float Float | Rectangle Float Float Float Float deriving (Show)

ghci> Circle 10 20 5
Circle 10.0 20.0 5.0
```

### 作为函数使用

因为构造函数也是函数，所以我们可以把它当成普通函数来使用

```haskell
ghci> map (Circle 10 20) [4,5,6,6]
[Circle 10.0 20.0 4.0,Circle 10.0 20.0 5.0,Circle 10.0 20.0 6.0,Circle 10.0 20.0 6.0]
```

### 使用子类型

我们还可以在定义类型的时候使用其它子类型

```haskell
data Point = Point Float Float deriving (Show)
data Shape = Point Float | Rectangle Float Float deriving (Show)
```

> 这里Point 类型的构造函数和自己的类型名字相同，虽然名字相同但它们是两个东西， 一个是表示类型，一个为构造函数

计算函数做对应修改

```haskell
surface :: Shape -> Float
surface (Circle _ r) = pi * r ^ 2
surface (Rectangle (Point x1 y1) (Point x2 y2)) = (abs $ x2 - x1) * (abs $ y2 - y1)

```

还可以定义辅助函数来帮助创建

```haskell
baseCircle :: Float -> Shape
baseCircle r = Circle (Point 0 0) r

baseRect :: Float -> Float -> Shape
baseRect width height = Rectangle (Point 0 0) (Point width height)
```

### 类型导出

类型的导出类似于模块的导出

```haskell
module Shapes
( Point(..)
, Shape(..)
, surface
, nudge
, baseCircle
, baseRect
) where
```

我们可以选择不导出任何 Shape 的值构函数，这一来使用我们模块的人就只能用辅助函数 baseCircle 和 baseRect 来得到 Shape 了。Data.Map 就是这一套，没有 Map.Map [(1,2),(3,4)] 而是使用Map.fromList 这样的辅助函数得到 map

## 记录表达式

如果我们需要表述一个人的 姓、名、年龄、身高、电话号码等  
如果直接表示 `data Person = Person String String Int Float String String deriving (Show)` 在构建和取数据时会非常麻烦  
这时我们可以使用 Record  

```haskell
data Person = Person {
  firstName:: String,
  lastName:: String,
  age:: Int,
  height:: Float,
  phone:: String,
  flavor:: String
} deriving (Show)
```

通过 Record Syntax，Haskell 会自动生这些函数：firstName, lastName, age, height, phoneNumber 和 flavor。

```haskell
ghci> :t flavor
flavor :: Person -> String
ghci> :t firstName
firstName :: Person -> String
```

同时在构造方式和打印内容上也有区别

```haskell
data Car = Car {company :: String, model :: String, year :: Int} deriving (Show)
ghci> Car {year=1967, company="Ford", model="Mustang"}
Car {company = "Ford", model = "Mustang", year = 1967}
```

## 类型参数

使用类型参数，在通过构造函数传入的参数的类型，决定了构造出的值的类型  
同时在使用类型时，可以通过传入类型参数，来决定构造函数传入的数据类型

```haskell
data Maybe a = Nothing | Just a
```

```haskell
ghci> Just "Haha"
Just "Haha"
ghci> Just 84
Just 84
ghci> :t Just "Haha"
Just "Haha" :: Maybe [Char]
ghci> :t Just 84
Just 84 :: (Num t) => Maybe t
ghci> :t Nothing
Nothing :: Maybe a
ghci> Just 10 :: Maybe Double
Just 10.0
```

> 这里 Nothing 的类型为 Maybe a，所以函数的参数类型 不论是Maybe Int 还是 Maybe Chart 都可以传入 Nothing
> 就像 5 可以是 Int 也可以是 Double， 空List [] 的类型是 [a]

使用类型参数，我们可以构造出不关心我们要处理的值的具体类型的数据类型，使我们的类型更加通用。  
比如说一个容器类型，就和适合定义为包含类型参数的类型，但是并不是所有类型都适合定义为需要传入类型参数的类型，比如用来描述具体对象的类型如Car  

### 添加类型约束

我们还可以在类型参数上加上类型约束，比如

```haskell
data (Ord k) => Map k v = ...
```

但是在haskell中并不建议这样去做，因为使用时不论我们需不需要用到约束我们都需要加上约束。其实完全可以在使用时加到类型签名中

## 派生实例

类型类就是定义了某些行为的接口  （它与类不同，更加类似于接口）
比如 Eq 类就定义了判定相等性的行为，Int类型是它的实例，所以Int的值可以判断相等性  

前面提到的派生关键字 deriving, 可以在我们构建类型时自动地给我们的类型加上相应的行为  
它们一般都是基于构造函数来做的相应行为，比如Eq 比较大小，构造函数在前面的对应值的大小要小于后面的值,所以False 一定比 True 小

```haskell
data Bool = False | True deriving (Ord)

ghci> True `compare` False
GT
ghci> True > False
True
ghci> True < False
False
```

而类似于 Just (*3) > Just (*2) 这样的比较是不行的，因为*3 *2 都是函数，而函数不属于Ord类 不能比较

## 类型别名

类型别名可以给类型提供了不同的名字，让我们的代码更容易理解  
在写类型签名时 `[Char]` 和 `String` 等价，可以互换。这就是由类型别名实现的。

```haskell
type String = [Char]

type PhoneNumber = String
type Name = String
type PhoneBook = [(Name,PhoneNumber)]

inPhoneBook :: Name -> PhoneNumber -> PhoneBook -> Bool
inPhoneBook name pnumber pbook = (name,pnumber) `elem` pbook
```

这里如果不用类型别名，我们函数的类型声明就只能是 String -> String -> [(String ,String)] -> Bool 了。在这里使用类型别名是为了让类型声明更加易读

### 类型别名也是可以有参数的

比如想表示一个可以接受任意类型的list

```haskell
type AssocList k v = [(k,v)]
```

### 不全调用类型

用不全的类型参数调用类型构造函数就可以得到一个不全调用的类型构造函数（类型构造函数不同于值构造函数，它是用于对类型的构造）  

如果我们要一个表示从整数到某东西间映射关系的类型

```haskell
type IntMap v = Map Int v

-- 或者
type IntMap = Map Int
```

如果要使用它，你可以用 qualified import 来导入 Data.Map。这时，类型构造函数前面必须得加上模块名。所以应该写个 type IntMap = Map.Map Int

> 类型构造函数和值构造函数的区别, 类型构造函数是用来构造具体类型的(等号前面那个), 值构造函数用来构造类型所对的值(等号后面的)

### Either

Maybe 是最常见的表示可能失败的计算的类型了, 但是Maybe有一个不足就是它只能表示失败了，失败的原因是办法表示的，不能携带更多的信息  
当我们需要知道具体的原因的时候，就需要使用 Either了，它用 a 来表示可能的错误的类型，用 b 来表示一个成功运算的类型， 即错误一律用 Left 值构造函数，而结果一律用 Right

```haskell
data Either a b = Left a | Right b deriving (Eq, Ord, Read, Show)
```

## 递归结构类型

当一个类型的构造函数使用了自己的类型时，它就是递归结构类型

```haskell
data List a = Empty | Cons a (List a) deriving (Show, Read, Eq, Ord)
```

这里构造函数 Cons 的第二个参数使用了List a 类型，即list里面还可以放入list， 这个放入的list可以是 Empty 也可以是另一个list

```haskell
ghci> Empty
Empty
ghci> 5 `Cons` Empty
Cons 5 Empty
ghci> 4 `Cons` (5 `Cons` Empty)
Cons 4 (Cons 5 Empty)
ghci> 3 `Cons` (4 `Cons` (5 `Cons` Empty))
Cons 3 (Cons 4 (Cons 5 Empty))
```

## 类型类

类型类 类似于接口， 它定义了一些行为（如 比较 排序 打印）， 我们会把满足这类性质的类型定义为该类型类的实例  
typeClass 的行为是由定义的函数来描述

Eq 的定义

```haskell
class Eq a where
  (==) :: a -> a -> Bool
  (/=) :: a -> a -> Bool
  {-# MINIMAL (==) | (/=) #-}
        -- Defined in ‘GHC.Classes’
```

这里的 a 就是我在定义实例时的类型，minimal 是要实现的最少的函数

```haskell
data TrafficLight = Red | Yellow | Green
```

### 实现一个类型类

```haskell
instance Eq TrafficLight where
    Red == Red = True
    Green == Green = True
    Yellow == Yellow = True
    _ == _ = False
```

在实现时我们使用 instance 关键字来定义 Eq的实例， 这里的TrafficLight就是class 那里的a  
这里我们只实现了 ==，这里也使用了模式匹配

```haskell
instance Show TrafficLight where
    show Red = "Red light"
    show Yellow = "Yellow light"
    show Green = "Green light"

ghci> Red == Red
True
ghci> Red == Yellow
False
ghci> Red `elem` [Red, Yellow, Green]
True
ghci> [Red, Yellow, Green]
[Red light,Yellow light,Green light]
```

### 子类型类

typeclass 定义成其他 typeclass 的 subclass（子类型类）

```haskell
class (Eq a) => Num a where
   ...
```

这里我们使用了类型约束， 类型约束（Eq a）可以在很多地方去使用。  
这里的表述就相当于，在一个类型实现Num类型类前，必须先实现Eq类型类
相当于我们在使用Num类型类的类型时，它的值默认是可以使用 == 作比较的

### 类型构造函数实现

类似于Maybe这种 不是一个具体类型，而属于一个类型构造函数的,可以这样定义

```haskell
instance (Eq m) => Eq (Maybe m) where
    Just x == Just y = x == y
    Nothing == Nothing = True
    _ == _ = False
```

我们可以先给类型构造函数传入一个参数，使之成为具体的类型 Maybe m  
同时我们要比较Maybe里面的元素相等， 所以对于参数m也必须是Eq类型类下的(Eq m)
> 这里需要注意区分, 类型类的类型约束是限制需要的子类型, 类型类实现的类型约束是限制要实现的类型的限制
> 使用:info 可以查看类型、类型类的信息

## 构建自己的类型类

通过类型类来实现javascript 的Boolean默认类型转换

```haskell
-- 定义类型类
class YesNo a where
    yesno :: a -> Bool
```

```haskell
-- Int
instance YesNo Int where
    yesno 0 = False
    yesno _ = True

-- [a]
instance YesNo [a] where
    yesno [] = False
    yesno _ = True

-- Bool
instance YesNo Bool where
    yesno = id

-- Maybe a
instance YesNo (Maybe a) where
    yesno (Just _) = True
    yesno Nothing = False
```

```haskell
ghci> yesno $ length []
False
ghci> yesno "haha"
True
ghci> yesno ""
False
ghci> yesno $ Just 0
True
ghci> yesno True
True
```

## 函子类型类（Functor typeClass）

```haskell
class Functor f where
    fmap :: (a -> b) -> f a -> f b
```

函子类型类描述的是可以被map的类型
